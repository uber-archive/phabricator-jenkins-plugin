plugins {
  id 'java'
  id 'checkstyle'
  id 'org.jenkins-ci.jpi' version '0.27.0'
  id 'net.saliman.cobertura' version '2.5.4'
  id 'com.github.kt3k.coveralls' version '2.8.2'
}

group = 'org.jenkins-ci.plugins'
archivesBaseName = 'phabricator-plugin'
version = '2.1.3'
description = 'Integrate Jenkins with Phabricator Differentials and Uberalls'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

jenkinsPlugin {
  coreVersion = '2.6'
  displayName = 'Phabricator Differential Plugin'
  url = 'https://wiki.jenkins-ci.org/display/JENKINS/Phabricator+Differential+Plugin'
  gitHubUrl = 'https://github.com/uber/phabricator-jenkins-plugin'
  shortName = 'phabricator-plugin'
  maskClasses = 'org.apache.http.'

  developers {
    developer {
      id 'ai'
      name 'Aiden Scandella'
      email 'sc@ndella.com'
    }
  }

  licenses {
    license {
      name 'MIT License'
      url 'http://opensource.org/licenses/MIT'
    }
  }
}

dependencies {
  compile 'org.apache.httpcomponents:httpclient:4.5.3'
  jenkinsPlugins 'org.jenkins-ci.plugins:credentials:1.22@jar'

  optionalJenkinsPlugins 'org.jenkins-ci.plugins:junit:1.6@jar'
  optionalJenkinsPlugins 'org.jenkins-ci.plugins:cobertura:1.11@jar'
  optionalJenkinsPlugins 'org.jenkins-ci.plugins:jacoco:2.2.1@jar'
  providedCompile 'org.jacoco:org.jacoco.report:0.7.8'

  testCompile 'junit:junit:4.12'
  testCompile 'org.apache.httpcomponents:httpclient:4.3:tests'
  testCompile 'net.sf.trove4j:trove4j:3.0.3'
  testCompile('org.mockito:mockito-core:1.10.19') {
    exclude module: 'hamcrest-core'
  }
  testCompile 'org.powermock:powermock-api-mockito:1.6.2'
  testCompile 'org.powermock:powermock-module-junit4:1.6.2'
  testCompile 'org.powermock:powermock-classloading-xstream:1.6.2'
  testCompile 'org.jenkins-ci.plugins:jacoco:2.2.1'

  jenkinsTest 'org.jenkins-ci.main:jenkins-war:2.6@war'
  jenkinsTest 'org.jenkins-ci.plugins:matrix-project:1.4@jar'
  jenkinsTest 'org.jenkins-ci.main:jenkins-test-harness:2.6@jar'
}

configurations.all {
  if (it.name.toLowerCase().contains("test")) {
    resolutionStrategy {
      force 'org.apache.httpcomponents:httpclient:4.3:tests'
      force 'xml-apis:xml-apis:1.4.01'
    }
  }
}

cobertura.coverageFormats = ['html', 'xml']
