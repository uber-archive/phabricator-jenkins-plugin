plugins {
  id 'java'
  id 'checkstyle'
  id 'org.jenkins-ci.jpi' version '0.38.0'
  id 'net.saliman.cobertura' version '2.6.1'
  id 'com.github.kt3k.coveralls' version '2.12.0'
}

group = 'org.jenkins-ci.plugins'
archivesBaseName = 'phabricator-plugin'
version = '2.1.6'
description = 'Integrate Jenkins with Phabricator Differentials and Uberalls'

sourceCompatibility = 1.11
targetCompatibility = 1.11

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

jenkinsPlugin {
  coreVersion = '2.414.3'
  displayName = 'Phabricator Differential Plugin'
  url = 'https://wiki.jenkins-ci.org/display/JENKINS/Phabricator+Differential+Plugin'
  gitHubUrl = 'https://github.com/uber/phabricator-jenkins-plugin'
  shortName = 'phabricator-plugin'
  maskClasses = 'org.apache.http.'

  developers {
    developer {
      id 'ai'
      name 'Aiden Scandella'
      email 'sc@ndella.com'
    }
  }

  licenses {
    license {
      name 'MIT License'
      url 'http://opensource.org/licenses/MIT'
    }
  }
}

dependencies {
  compile 'org.apache.httpcomponents:httpclient:4.5.13'

  jenkinsPlugins 'io.jenkins.plugins:checks-api:1.8.1@jar'
  jenkinsPlugins 'io.jenkins.plugins:plugin-util-api:2.20.0@jar'
  jenkinsPlugins 'org.jenkins-ci.plugins:credentials:2.6.2@jar'
  jenkinsPlugins 'org.jenkins-ci.plugins:display-url-api:2.3.7@jar'

  optionalJenkinsPlugins 'org.jenkins-ci.plugins:junit:1.63@jar'
  optionalJenkinsPlugins 'org.jenkins-ci.plugins:cobertura:1.17@jar'
  optionalJenkinsPlugins 'org.jenkins-ci.plugins:jacoco:3.3.2@jar'
  providedCompile 'org.jacoco:org.jacoco.report:0.8.8'

  testCompile 'junit:junit:4.13.2'
  testCompile 'org.apache.httpcomponents:httpclient:4.5.13:tests'
  testCompile 'net.sf.trove4j:trove4j:3.0.3'
  testCompile('org.mockito:mockito-core:1.10.19') {
    exclude module: 'hamcrest-core'
  }
  testCompile 'org.powermock:powermock-api-mockito2:2.0.9'
  testCompile 'org.powermock:powermock-module-junit4:2.0.9'
  testCompile 'org.powermock:powermock-classloading-xstream:2.0.9'
  testCompile 'org.jenkins-ci.plugins:jacoco:3.3.2'

  jenkinsTest 'org.jenkins-ci.main:jenkins-war:2.414.3@war'
  jenkinsTest 'org.jenkins-ci.plugins:matrix-project:1.20@jar'
  jenkinsTest 'org.jenkins-ci.main:jenkins-test-harness:1912.v0cdf15450b_fb@jar'
}

configurations.all {
  if (it.name.toLowerCase().contains("test")) {
    resolutionStrategy {
      force 'org.apache.httpcomponents:httpclient:4.5.13:tests'
      force 'xml-apis:xml-apis:1.4.01'
    }
  }
}

cobertura.coverageFormats = ['html', 'xml']
